# Distributed under the OSI-approved BSD 3-Clause License.  See accompanying
# file Copyright.txt or https://cmake.org/licensing for details.

include("@MDT_DEPLOY_UTILS_INSTALL_SCRIPT_HELPERS@")

set(CMAKE_MESSAGE_LOG_LEVEL @CMAKE_MESSAGE_LOG_LEVEL@)
# set(CMAKE_MESSAGE_LOG_LEVEL VERBOSE)
# set(CMAKE_MESSAGE_LOG_LEVEL DEBUG)

message(VERBOSE "Running mdt_deploy_application() script")

set(targetFile "$<TARGET_FILE:@ARG_TARGET@>")
message(DEBUG "targetFile: ${targetFile}")

message(VERBOSE "DESTDIR: $ENV{DESTDIR}")
message(VERBOSE "CMAKE_INSTALL_PREFIX: ${CMAKE_INSTALL_PREFIX}")
message(VERBOSE "CMAKE_PREFIX_PATH: @CMAKE_PREFIX_PATH@")
message(DEBUG "CMAKE_CXX_COMPILER: @CMAKE_CXX_COMPILER@")

set(IS_MSVC @MSVC@)
message(DEBUG "Is MSVC: ${IS_MSVC}")

message(DEBUG "MDT_INSTALL_PREFIX_WITH_DESTDIR: ${MDT_INSTALL_PREFIX_WITH_DESTDIR}")

set(INSTALL_IS_UNIX_SYSTEM_WIDE @MDT_DEPLOY_APPLICATION_INSTALL_SCRIPT_INSTALL_IS_UNIX_SYSTEM_WIDE@)
if(INSTALL_IS_UNIX_SYSTEM_WIDE)
  message(DEBUG "Install is Unix system wide")
  set(overwriteBehavior keep)
  set(removeRpathOptionArgument --remove-rpath)
else()
  message(DEBUG "Install is NOT Unix system wide")
  set(overwriteBehavior overwrite)
  set(removeRpathOptionArgument)
endif()

message(VERBOSE "overwriteBehavior: ${overwriteBehavior}")
message(VERBOSE "removeRpathOptionArgument: ${removeRpathOptionArgument}")

set(compilerLocationArguments)
if(IS_MSVC)
  message(DEBUG "Is MSVC, use compiler finder to locate redist dirs")
  set(compilerLocationArguments --compiler-location "compiler-path=@CMAKE_CXX_COMPILER@")
endif()

set(logLevelArguments)
if(CMAKE_MESSAGE_LOG_LEVEL)
  set(logLevelArguments --log-level ${CMAKE_MESSAGE_LOG_LEVEL})
endif()

string(REPLACE ";" "," searchPrefixPathList "@CMAKE_PREFIX_PATH@")
message(DEBUG "searchPrefixPathList: ${searchPrefixPathList}")

execute_mdtdeployutils(
  MDTDEPLOYUTILS_EXECUTABLE "@MDT_DEPLOY_UTILS_INSTALL_SCRIPT_MDTDEPLOYUTILS_EXECUTABLE@"
  RUNTIME_ENV "@MDT_DEPLOY_UTILS_INSTALL_SCRIPT_MDTDEPLOYUTILS_RUNTIME_ENV@"
  ARGUMENTS --logger-backend cmake ${logLevelArguments}
            deploy-application
            --shlib-overwrite-behavior ${overwriteBehavior}
            ${removeRpathOptionArgument}
            --search-prefix-path-list "${searchPrefixPathList}"
            ${compilerLocationArguments}
            --runtime-destination "@MDT_DEPLOY_APPLICATION_INSTALL_SCRIPT_RUNTIME_DESTINATION@"
            --library-destination "@MDT_DEPLOY_APPLICATION_INSTALL_SCRIPT_LIBRARY_DESTINATION@"
            "${targetFile}"
            "${MDT_INSTALL_PREFIX_WITH_DESTDIR}"
)
